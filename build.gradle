plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/central' }
    maven { url 'https://repo.codemc.io/repository/nms/' }
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include '*.yml'
        expand(rootProject.properties)
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT'

    // Library loader
    implementation 'com.saicone.ezlib:ezlib:1.2.3'
    implementation 'com.saicone.ezlib:loader:1.2.3'
    compileOnly 'com.saicone.ezlib:annotations:1.2.3'
    annotationProcessor 'com.saicone.ezlib:annotations:1.2.3'

    // Implementation
    compileOnly 'com.saicone.settings:settings:14d88b0f12'
    compileOnly 'com.saicone.settings:settings-yaml:14d88b0f12'
    compileOnly 'com.saicone.delivery4j:delivery4j:1.0'
    compileOnly 'com.saicone.delivery4j:delivery4j-hikari:1.0'
    compileOnly 'com.saicone.delivery4j:delivery4j-redis:1.0'
    compileOnly 'at.favre.lib:bcrypt:0.9.0'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
    compileOnly 'com.github.zafarkhaja:java-semver:0.9.0'

    compileOnly 'org.jetbrains:annotations:24.1.0'

    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
}

compileJava {
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

jar {
    enabled = false
    dependsOn (shadowJar)
}

shadowJar {
    archiveClassifier.set('')
    relocate 'com.saicone.ezlib', project.group + '.ezlib'
    relocate 'com.saicone.settings', project.group + '.libs.settings'
    relocate 'org.yaml.snakeyaml', project.group + '.libs.snakeyaml'
    relocate 'com.saicone.delivery4j', project.group + '.libs.delivery4j'
    relocate 'com.zaxxer.hikari', project.group + '.libs.hikari'
    relocate 'redis.clients.jedis', project.group + '.libs.jedis'
    relocate 'at.favre.lib.crypto.bcrypt', project.group + '.libs.bcrypt'
    relocate 'at.favre.lib.bytes', project.group + '.libs.bytes'
    relocate 'com.github.zafarkhaja.semver', project.group + '.libs.semver'
    relocate 'org.bstats', project.group + '.libs.bstats'
    minimize()
}