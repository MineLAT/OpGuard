plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/central' }
    maven { url 'https://repo.codemc.io/repository/nms/' }
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand(rootProject.properties)
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT'

    implementation 'org.bstats:bstats-bukkit:2.2.1'
    implementation 'com.github.zafarkhaja:java-semver:0.9.0'
    implementation 'at.favre.lib:bcrypt:0.9.0'

    compileOnly 'pl.tlinkowski.annotation:pl.tlinkowski.annotation.basic:0.2.0'

    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
}

compileJava {
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

jar {
    enabled = false
    dependsOn (shadowJar)
}

shadowJar {
    archiveClassifier.set('')
    relocate 'at.favre', project.group + '.shaded.at.favre'
    relocate 'com.github', project.group + '.shaded.com.github'
    relocate 'org.bstats', project.group + '.shaded.org.bstats'
    minimize()
}